% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Preliminaries}

<<Preliminaries>>=
begin :: Preliminaries

<<Definition: valid radix>>

<<Cluster: existence of valid radix number>>

<<Registration: valid radix implies positive>>

<<Reserve: $b$ refers to a valid radix real number>>

<<Theorem: each $x$ has an $N$ such that $b^{N}\leq x<b^{N+1}$>>
@

\subsection{Valid radix for a non-integer base of numeration}
We will give a generalization of the positional numeral system for a
radix $\beta\in\RR$ such that $\beta>1$. Mizar formalizes positional
number systems in \texttt{[NUMERAL1]}, and we will be consistent with
the notation taken there.

Before getting ahead of ourselves, we need to formalize the notion that
a real number is a valid radix --- that is to say, that $\beta>1$.

\begin{definition}
Let $\beta\in\RR$. We define the attribute $\beta$ is a \define{Valid Radix}
if $\beta>1$.
\end{definition}

<<Definition: valid radix>>=
definition
  let IT be Real;
  attr IT is valid_radix means :Def1:
  IT > 1;
end;
@ %def valid_radix

\begin{voc}
We need to add [[valid_radix]] as an attribute to our vocabulary file.
\end{voc}

<<DICT/float-1.voc>>=
Vvalid_radix
@ 

\begin{registration}
We observe that there exists a valid radix real number (which allows us
to use [[valid_radix]] as an adjective).
\end{registration}

\begin{proof}[Proof sketch]
We just need to prove there exists a real number $x$ such that $x$ is a
valid radix. For example, $x=2$ is such a number.
\end{proof}

<<Cluster: existence of valid radix number>>=
registration
  cluster valid_radix for Real;
  existence
  proof
    take x=2;
    thus thesis by Def1;
  end;
end;
@

\begin{registration}
We observe that valid radix numbers are positive.
\end{registration}

\begin{proof}[Proof sketch]
We just need to prove that for each real number $x$ such that $x$ is a
valid radix that $x$ is positive. But this follows from the definition
of ``positive'' ([[XXREAL_0:def 6]]) and the definition for a valid radix.
\end{proof}

<<Registration: valid radix implies positive>>=
registration
  cluster valid_radix -> positive for Real;
  coherence by Def1, XXREAL_0:def 6;
end;
@

\subsection{Non-integer base of numeration}
Now, when we consider a non-integer base of numeration $\beta>1$ for a
real number $x\in\RR$, we are considering a [possibly infinite] sequence
of finite expansions
\begin{equation}
x = d_{0}d_{1}\cdots d_{n-1}.d_{n}\cdots d_{n+m-1}\dots
\end{equation}
where the $n^{\text{th}}$ entry in the sequence is
$D_{n} = (d_{0},d_{1},\dots,d_{n-1})$. This describes the truncated expansion to
$n$ digits.

The \textsc{ieee} standard, however, works in scientific notation,
writing something like
\begin{equation}
x = d_{0}.d_{1}\cdots d_{n+m-1}\times\beta^{n-1},
\end{equation}
or as the product of an integer with $\beta^{-m}$,
\begin{equation}
x = d_{0}d_{1}\cdots d_{n+m-1}\times\beta^{-m}.
\end{equation}
How do we determine these digits $d_{j}$?

For any positive real number $x>0$, we observe there exists an
integer $N$ such that $\beta^{N}\leq x<\beta^{N+1}$. Then
\begin{equation}
d_{0}=\lfloor x/\beta^{N}\rfloor,\quad\mbox{and}\quad r_{0}=(x/\beta^{N})-d_{0},
\end{equation}
and inductively, for any natural number $j$,
\begin{equation}
d_{j+1}=\lfloor\beta r_{j}\rfloor,\quad\mbox{and}\quad r_{j+1}=(\beta r_{j})-d_{j+1}.
\end{equation}
This produces a canonical sequence of digits which is precisely the
$\beta$-expansion of $x$:
\begin{equation}
x = \sum^{\infty}_{j=0}d_{j}\beta^{N-j}.
\end{equation}
We need to construct the sequence $\{D_{n}\}_{n\in\NN_{0}}$ consisting
of the finite sequences $D_{n}=(d_{0},\dots,d_{n})$ so we might use it
in determining the floating-point number corresponding to $x$ in base
$\beta$.

Mizar implements $x^{y}$ for any $x,y\in\RR$ as ``[[x to_power y]]'' in
Definition [[POWER:def 2]]. For our interests, we will be considering
$\beta^{n}$ for $n\in\ZZ$, which is implemented as ``[[beta #Z n]]''
as defined in Definition [[PREPOWER:def 3]].

\begin{reserve}
We will use $b$ to refer to any valid radix real number.
\end{reserve}

<<Reserve: $b$ refers to a valid radix real number>>=
reserve b for valid_radix Real;
@ 

\begin{theorem}[Existence]
For any positive real $x\in\RR$, there exists an integer $N\in\ZZ$
such that $b^{N}\leq x<b^{N+1}$.
\end{theorem}

\begin{proof}
Take $N=\lfloor\log_{b}(x)\rfloor$.
We see that $x=b^{\log_{b}(x)}$, of course.
We can relegate the subproof of the claim $b^{N}\leq x$ to its own niche.
Then we use the fact that $\log_{b}(x)<N+1$ to prove $x<b^{N+1}$.
\end{proof}

<<Theorem: each $x$ has an $N$ such that $b^{N}\leq x<b^{N+1}$>>=
theorem
  for x being positive Real
  ex N being Integer
  st b to_power N <= x & x < b to_power (N + 1)
proof
  let x be positive Real;
  take N = [\ log(b, x) /];
  b > 0 & b <> 1 by Def1; then
  A1: x = b to_power log(b, x) by POWER:def 3;
  thus b to_power N <= x
  <<Subproof: $b^{N}\leq x$>>
  log(b,x) < N + 1 & b > 1 by Def1, INT_1:29;
  hence x < b to_power (N + 1) by A1, POWER:39;
end;
@

\begin{proof}[Subproof ($b^{N}\leq x$)]
This is a proof by cases, since either $N=\log_{b}(x)$ or $N<\log_{b}(x)$.

In the first situation, when $N=\log_{b}(x)$, it immediately follows 
from the identity $b^{\log_{b}(x)}=x$.

In the second situation, when $N<\log_{b}(x)$, since $b>1$ is a valid radix,
we obtain the result using Theorem [[POWER:39]] which proves for any
real numbers $y<z$ and $b>1$ we have $b^{y}<b^{z}$.
\end{proof}

<<Subproof: $b^{N}\leq x$>>=
proof
  N = log(b,x) or N < log(b,x) by INT_1:25, INT_1:26; then
  per cases;
  suppose N = log(b, x);
    hence thesis by A1;
  end;
  suppose N < log(b, x);
    then N < log(b,x) & b > 1 by Def1;
    hence thesis by A1, POWER:39;
  end;
end;
@

