:: Floating-point Arithmetic
:: by Alexander M. Nelson
::
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ
 vocabularies NUMBERS, ORDINAL1, CARD_1, SUBSET_1, XBOOLE_0, ARYTM_2, ARYTM_3,
   ZFMISC_1, XCMPLX_0, XREAL_0, XXREAL_0, TARSKI, FUNCT_7, RELAT_1,
   REAL_1, INT_1, PREPOWER, POWER, ARYTM_1, NAT_1, COUNTERS,
   COMPLEX1, FINSET_1, NEWTON, PRE_TOPC, FUZNUM_1,
   FLOAT_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, ARYTM_2, ARYTM_3,
   XXREAL_3, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, NEWTON, INT_1, COUNTERS, COMPLEX1, FINSET_1,
   PREPOWER, POWER;
 constructors ARYTM_2, ARYTM_3, NUMBERS, XCMPLX_0, XXREAL_0, XXREAL_3, XREAL_0, INT_1,
   COMPLEX1, NEWTON, FINSET_1, COUNTERS, PREPOWER, POWER;
 registrations ORDINAL1, XBOOLE_0, COUNTERS, NUMBER01,
   XCMPLX_0, XXREAL_0, XREAL_0, XXREAL_3, FINSET_1, RAT_1, INT_1,
   FOMODEL0, NEWTON;
 requirements REAL, SUBSET, BOOLE, NUMERALS, ARITHM;
 theorems ARYTM_2, ARYTM_3, CARD_1, COMPLEX1, COUNTERS, ENUMSET1, FINSET_1, INT_1, NUMBERS, ORDINAL1, ORDINAL2, ORDINAL3, POWER,
   TARSKI, XBOOLE_0, XBOOLE_1, XCMPLX_0, XCMPLX_1, XREAL_0, XREAL_1, XXREAL_0, XXREAL_3, XTUPLE_0, ZFMISC_1;

begin :: Preliminaries

definition
  let IT be Real;
  attr IT is valid_radix means :Def1:
  IT > 1;
end;

registration
  cluster valid_radix for Real;
  existence
  proof
    take x=2;
    thus thesis by Def1;
  end;
end;

registration
  cluster valid_radix for Nat;
  existence
  proof
    take 2;
    thus thesis;
  end;
end;

registration
  cluster valid_radix -> positive for Real;
  coherence by Def1, XXREAL_0:def 6;
end;

registration
  let x be positive Real;
  let y be Real;
  cluster x to_power y -> positive;
  coherence by POWER:34;
end;

theorem Th1:
  for x,y,z being Real
  st x <= y & z > 1
  holds z to_power x <= z to_power y
proof
  let x,y,z be Real;
  assume A1: x <= y;
  assume A2: z > 1;
  x < y or x = y by A1, XXREAL_0:1;
  hence thesis by A2, POWER:39;
end;

theorem ThNatPowers:
  for x being Real
  for y being Real
  st x in NAT & y in NAT
  holds x to_power y in NAT
proof
  let x be Real;
  let y be Real;
  assume A1: x in NAT;
  assume A2: y in NAT;
  reconsider X=x, Y=y as Nat by A1,A2;
  X to_power Y = X |^ Y by POWER:41;
  hence x to_power y in NAT by ORDINAL1:def 12;
end;

reserve b for valid_radix Real;

theorem Th1b:
  for x being positive Real
  for N being Integer
  holds (b to_power N <= x & x < b to_power (N + 1))
  iff N = [\ log(b, x) /]
proof
  let x be positive Real;
  let N be Integer;
  thus (b to_power N <= x & x < b to_power (N + 1)) implies N = [\ log(b, x) /]
  proof
    assume A1: b to_power N <= x;
    assume A2: x < b to_power (N + 1);
    A3: b <> 1 & b > 1 by Def1;
    
    A4: N <= log(b, x)
    proof
      b to_power N = x or b to_power N < x by A1, XXREAL_0:1;
      then per cases;
      suppose b to_power N = x;
        hence thesis by A3, POWER:def 3;
      end;
      suppose A5: b to_power N < x;
        A6: b to_power N > 0 by POWER:34;
        then N = log(b, b to_power N) by A3, POWER:def 3;
        hence thesis by A3, A5, A6, POWER:57;
      end;
    end;
    log(b,x) < N + 1
    proof
      b to_power (N + 1) > 0 by POWER:34;
      then N + 1 = log(b, b to_power (N + 1)) by A3, POWER:def 3;
      hence log(b,x) < (N+1) by A2, A3, POWER:57;
    end;
    then log(b,x) - 1 < (N + 1) - 1 by XREAL_1:14;
    hence thesis by A4, INT_1:def 6;
  end;
  thus N = [\ log(b, x) /] implies (b to_power N <= x & x < b to_power (N + 1))
  proof
    assume N = [\ log(b, x) /];
    then N <= log(b, x) & log(b,x) - 1 < N by INT_1:def 6;
    then N <= log(b, x) & log(b,x) - 1 + 1 < N + 1 by XREAL_1:8;
    then A1: N <= log(b, x) & log(b, x) < N + 1;
    A2: b to_power N <= b to_power (log(b,x))
      & b to_power (log(b,x)) < b to_power (N + 1)
    proof
      B1: b > 1 by Def1;
      thus b to_power N <= b to_power (log(b,x))
      proof
        per cases by A1, XXREAL_0:1;
        suppose N = log(b, x);
          hence thesis;
        end;
        suppose N < log(b, x);
          hence thesis by B1, POWER:39;
        end;
      end;
      thus b to_power (log(b,x)) < b to_power (N + 1) by A1, B1, POWER:39;
    end;
    b > 0 & b <> 1 by Def1;
    hence (b to_power N <= x & x < b to_power (N + 1)) by A2, POWER:def 3;
  end;
end;

theorem Th2:
  for x being positive Real
  holds 1 <= x * (b to_power (-[\ log(b, x) /]))
           & x * (b to_power (-[\ log(b, x) /])) < b
proof
  let x be positive Real;
  set N = [\ log(b, x) /];
  A1: b > 0 & b <> 1 by Def1;
  A2: b to_power N > 0 by POWER:34;
  A3: (b to_power N) * (b to_power (- N))
     = b to_power (N + (- N)) by POWER:27
    .= 1 by POWER:24;

  thus 1 <= x * (b to_power (- N))
  proof
    set z = b to_power N;
    1 / z = b to_power (- N) by POWER:28;
    hence 1 <= x * (b to_power (- N)) by A2, A3, Th1b, XREAL_1:64;
  end;

  thus x * (b to_power (- N)) < b
  proof
    b to_power (N + 1) = (b to_power 1) * (b to_power N) by POWER:27
                      .= b * (b to_power N) by POWER:25;
    then B1: (b to_power (N + 1)) * (b to_power (- N))
     = b * ((b to_power N) * (b to_power (- N)))
    .= b by A3;
    0 < (b to_power (- N)) by POWER:34;
    hence x * (b to_power (- N)) < b by Th1b, B1, XREAL_1:68;
  end;
end;

definition
  func ExtINT -> Subset of ExtREAL equals
  INT \/ {-infty,+infty};
  coherence
  proof
    INT c= REAL & REAL c= ExtREAL by XBOOLE_0:def 8, NUMBERS:5,31; then
    A1: INT c= ExtREAL by XBOOLE_1:1;
    {-infty,+infty} c= ExtREAL by XBOOLE_1:7, XXREAL_0:def 4;
    hence thesis by A1, XBOOLE_1:8;
  end;
end;

theorem Th2:
  INT c< ExtINT & ExtNAT c< ExtINT & ExtINT c< ExtREAL
proof
  thus INT c< ExtINT
  proof
    +infty in {+infty,-infty} by TARSKI:def 2; then
    A1: +infty in ExtINT by XBOOLE_0:def 3;
    not +infty in INT;
    hence thesis by A1, XBOOLE_0:def 8, XBOOLE_1:7;
  end;
  thus ExtNAT c< ExtINT
  proof
    -1 in INT & not -1 in NAT & not -1 in {+infty} by INT_1:def 1;
    then A2: -1 in ExtINT & not -1 in ExtNAT by XBOOLE_0:def 3;
    NAT c= INT & {+infty} c= {-infty,+infty} by NUMBERS:17, ZFMISC_1:7;
    hence thesis by A2, COUNTERS:def 1, XBOOLE_1:13, XBOOLE_0:def 8;
  end;
  thus ExtINT c< ExtREAL
  proof
    not REAL c= INT by XBOOLE_0:def 10, NUMBERS:15,25;
    then consider x being object such that
    A3: x in REAL & not x in INT
    by TARSKI:def 3; 
    not x in {-infty,+infty} by A3, TARSKI:def 2;
    then x in ExtREAL & not x in ExtINT by A3, XXREAL_0:def 4, XBOOLE_0:def 3;
    hence thesis by XBOOLE_0:def 8;
  end;
end;

registration
  cluster ExtINT -> non empty infinite;
  coherence by Th2, FINSET_1:1, XBOOLE_0:def 8;
end;

definition
  let x be object;
  attr x is ext-integer means :Def2:
  x in ExtINT;
end;

registration
  cluster +infty -> ext-integer;
  coherence
  proof
    +infty in {+infty,-infty} by TARSKI:def 2;
    hence thesis by XBOOLE_0:def 3;
  end;
  cluster -infty -> ext-integer;
  coherence
  proof
    -infty in {+infty,-infty} by TARSKI:def 2;
    hence thesis by XBOOLE_0:def 3;
  end;
  cluster ext-integer -> ext-real for object;
  coherence;
  cluster ext-natural -> ext-integer for object;
  coherence
  proof
    let x be object;
    assume x is ext-natural;
    then x in ExtNAT by COUNTERS:def 2;
    hence thesis by Th2, TARSKI:def 3, XBOOLE_0:def 8;
  end;
end;

registration
  cluster zero ext-integer for object;
  existence
  proof
    take 0;
    thus thesis;
  end;
  cluster non zero ext-integer for object;
  existence
  proof
    take 1;
    thus thesis;
  end;
  cluster ext-integer for set;
  existence
  proof
    take 0;
    thus thesis;
  end;
  cluster -> ext-integer for Element of ExtINT;
  coherence;
  cluster -> ext-integer for Element of ExtNAT;
  coherence;
end;

definition
  mode ExtInt is ext-integer ExtReal;
end;

registration
  sethood of ExtInt
  proof
    take ExtINT;
    thus thesis by Def2;
  end;
end;

theorem Th4:
  for x being object
  holds x is ExtInt iff (x is Integer or x = -infty or x = +infty)
proof
  let x be object;
  thus x is ExtInt implies (x is Integer or x = -infty or x = +infty)
  proof
    assume A1: x is ExtInt;
    assume A2: not x is Integer & not x = -infty;
    x in INT \/ {-infty, +infty} by A1, Def2;
    then x in INT or x in {-infty, +infty} by XBOOLE_0:def 3;
    hence x = +infty by A2, TARSKI:def 2;
  end;
  thus (x is Integer or x = -infty or x = +infty) implies x is ExtInt
  proof
    assume x is Integer or x = -infty or x = +infty;
    then x in INT or x in {-infty,+infty} by INT_1:def 2, TARSKI:def 2;
    then x in INT \/ {-infty,+infty} by XBOOLE_0:def 3;
    hence x is ExtInt by Def2;
  end;
end;

registration
  cluster zero -> ext-integer for object;
  coherence;
  cluster integer -> ext-integer for object;
  coherence by Th4;
end;

registration
  cluster natural -> ext-integer for object;
  coherence;
end;

registration
  cluster positive for ExtInt;
  existence
  proof
    take 1;
    thus thesis;
  end;
  cluster non positive for ExtInt;
  existence
  proof
    take -1;
    thus thesis;
  end;
  cluster negative for ExtInt;
  existence
  proof
    take -1;
    thus thesis;
  end;
  cluster non negative for ExtInt;
  existence
  proof
    take 1;
    thus thesis;
  end;
end;

registration
  let m be ExtInt;
  cluster - m -> ext-integer;
  coherence
  proof
    per cases by Th4;
    suppose m is Integer;
      then reconsider M=m as Integer;
      -M is Integer;
      hence thesis by XXREAL_3:def 3;
    end;
    suppose m=-infty;
      hence thesis by XXREAL_3:def 3;
    end;
    suppose m=+infty;
      hence thesis by XXREAL_3:def 3;
    end;
  end;
end;

registration
  let m, n be ExtInt;
  cluster min(m, n) -> ext-integer;
  coherence by XXREAL_0:def 9;
  cluster max(m, n) -> ext-integer;
  coherence by XXREAL_0:def 10;
  cluster m + n -> ext-integer;
  coherence
  proof
    per cases by Th4;
    suppose A1: m is Integer;
      then reconsider M=m as Integer;
      per cases by Th4;
      suppose n is Integer;
        then reconsider N=n as Integer;
        M + N is Integer;
        hence thesis by XXREAL_3:def 2;
      end;
      suppose n=-infty;
        hence thesis by A1, XXREAL_3:def 2;
      end;
      suppose n=+infty;
        hence thesis by A1, XXREAL_3:def 2;
      end;
    end;
    suppose A2: m=-infty;
      per cases by Th4;
      suppose n is Integer;
        hence thesis by A2, XXREAL_3:def 2;
      end;
      suppose n=-infty;
        hence thesis by A2, XXREAL_3:def 2;
      end;
      suppose n=+infty;
        hence thesis by A2;
      end;
    end;
    suppose A3: m=+infty;
      per cases by Th4;
      suppose n is Integer;
        hence thesis by A3, XXREAL_3:def 2;
      end;
      suppose n=-infty;
        hence thesis by A3;
      end;
      suppose n=+infty;
        hence thesis by A3;
      end;
    end;
  end;
  cluster m * n -> ext-integer;
  coherence
  proof
    per cases by Th4;
    suppose m is Integer;
      then reconsider M=m as Integer;
      per cases by Th4;
      suppose n is Integer;
        then reconsider N=n as Integer;
        M * N is Integer;
        hence thesis by XXREAL_3:def 5;
      end;
      suppose B1: n=-infty;
        m is positive or m is negative or m is zero;
        hence thesis by B1, XXREAL_3:def 5;
      end;
      suppose B2: n=+infty;
        m is positive or m is negative or m is zero;
        hence thesis by B2, XXREAL_3:def 5;
      end;
    end;
    suppose A2: m=-infty;
      per cases by Th4;
      suppose n is Integer;
        n is positive or n is negative or n is zero;
        hence thesis by A2, XXREAL_3:def 5;
      end;
      suppose n=-infty;
        hence thesis by A2, XXREAL_3:def 5;
      end;
      suppose n=+infty;
        hence thesis by A2, XXREAL_3:def 5;
      end;
    end;
    suppose A3: m=+infty;
      per cases by Th4;
      suppose n is Integer;
        n is positive or n is negative or n is zero;
        hence thesis by A3, XXREAL_3:def 5;
      end;
      suppose n=-infty;
        hence thesis by A3, XXREAL_3:def 5;
      end;
      suppose n=+infty;
        hence thesis by A3;
      end;
    end;
  end;
end;

registration
  let m, n be ExtInt;
  cluster m - n -> ext-integer;
  coherence
  proof
    (- n) is ext-integer & m + (- n) is ext-integer &
    m - n = m + (- n) by XXREAL_3:def 4;
    hence thesis;
  end;
end;

registration
  cluster non negative -> ext-natural for ExtInt;
  coherence
  proof
    let x be ExtInt;
    assume A1: x is non negative;
    x is Integer or x = -infty or x = +infty by Th4;
    hence thesis by A1;
  end;
end;

theorem ThPosExtInt:
  for x being object
  holds x is positive ExtInt iff (x is positive Integer or x = +infty) by Th4;

theorem ThNegExtInt:
  for x being object
  holds x is negative ExtInt iff (x is negative Integer or x = -infty) by Th4;


begin :: 3. Floating-point formats

definition
  func -0 -> object equals [0,0];
  coherence;
end;

theorem Th4:
  for n being Nat
  holds [n,0] <> +infty & [n,0] <> -infty
proof
  let n be Nat;
  A1: n in REAL by NUMBERS:19, TARSKI:def 3, ORDINAL1:def 12;
  +infty = [n,0] implies contradiction
  proof
    assume A2: +infty = [n,0];
    then +infty = {{n}, {n,0}} by TARSKI:def 5;
    then n = {n} or n = {n,0};
    then per cases;
    suppose n = {n};
      hence contradiction by ZFMISC_1:140;
    end;
    suppose n = {n,0};
      then n in n or n in 0 by TARSKI:def 2;
      hence contradiction by ORDINAL1:def 13;
    end;
  end;
  hence [n,0] <> +infty;
  thus [n,0] <> -infty by A1, XXREAL_0:def 3, XTUPLE_0:1, ORDINAL1:def 13;
end;

theorem Th5:
  not -0 in ExtREAL
proof
  [0,0] <> +infty & [0,0] <> -infty by Th4;
  then not (-0 in REAL or -0 in {+infty, -infty})
  by NUMBERS:def 1, ZFMISC_1:56, TARSKI:def 2;
  hence thesis by NUMBERS:def 5, XBOOLE_0:def 3, XXREAL_0:def 2,def 3;
end;

Lm1:
  for n being Nat
  holds not [n,0] in RAT+
proof
  let n be Nat;
  assume A1: [n,0] in RAT+;
  n in omega by ORDINAL1:def 12;
  then 0 <> {} by A1, ARYTM_3:33;
  hence contradiction by ORDINAL1:def 13;
end;

Lm2:
  for a,b being Nat
  st a = {b}
  holds b = {}
proof
  let a,b be Nat;
  assume A1: a = {b};
  then consider c being Nat such that
  A2: a = succ c by CARD_1:36;
  {b} = c \/ {c} by A1, A2, ORDINAL1:def 1;
  then c = {} by ZFMISC_1:38, ZFMISC_1:140;
  then a = {} \/ {{}} by A2, ORDINAL1:def 1;
  hence thesis by A1, ZFMISC_1:3;
end;

Lm3:
  for i being Nat st i <> 0
  holds not {i} in RAT+
proof
  let i be Nat such that
  A1: i <> 0;
  assume A2: {i} in RAT+;
  then per cases by ARYTM_3:29;
  suppose {i} in omega;
    then {} in {i} by ORDINAL3:8;
    hence contradiction by A1, ORDINAL1:def 13, TARSKI:def 1;
  end;
  suppose ex a,b being Element of omega st ({i} = [a,b] &
    a,b are_coprime & b <> {} & b <> 1);
    then consider a,b being Element of omega such that
    A3: ({i} = [a,b] & a,b are_coprime & b <> {} & b <> 1);
    {i} = {{a}, {a,b}} by A3, TARSKI:def 5;
    then {a} = {a,b} by ZFMISC_1:5;
    then A4: a = b by ZFMISC_1:5;
    then {i} = {{b}, {b,b}} by A3, TARSKI:def 5
            .= {{b}, {b}} by ENUMSET1:29
            .= {{b}} by ENUMSET1:29;
    hence contradiction by A3, Lm2, ZFMISC_1:3;
  end;
end;

Lm4:
  for i being Nat st i <> 0
  holds not [i,0] in DEDEKIND_CUTS
proof
  let i be Nat such that
  A1: i <> 0;
  assume [i,0] in DEDEKIND_CUTS;
  then [i,0] in {A where A is Subset of RAT+ : for r being Element of RAT+
  st r in A holds ((for s being Element of RAT+ st s <=' r holds s in A)
                   & ex s being Element of RAT+ st (s in A & r < s)) }
  by ARYTM_2:def 1;
  then consider A being Subset of RAT+ such that
  A2: [i,0] = A
    & for r being Element of RAT+ st r in A
      holds (for s being Element of RAT+ st s <=' r holds s in A)
          & ex s being Element of RAT+ st s in A & r < s;
  A = {{i}, {i,0}} by A2, TARSKI:def 5;
  then {i} in RAT+ & {i,0} in RAT+ by ZFMISC_1:32;
  hence contradiction by A1, Lm3;
end;

theorem Th6:
  for i being Nat
  holds not [i,0] in ExtREAL
proof
  let i be Nat;
  A1: not [i,0] in REAL
  proof
    per cases;
    suppose i = 0;
      hence thesis by Th5, NUMBERS:def 5, XBOOLE_0:def 3;
    end;
    suppose A1: i <> 0;
      then not ([i,0] in RAT+ or [i,0] in DEDEKIND_CUTS) by Lm1,Lm4;
      then A2: not [i,0] in REAL+ by ARYTM_2:def 2, XBOOLE_0:def 3;
      not [i,0] in [:{0},REAL+:] by A1, ZFMISC_1:105;
      hence thesis by A2, XBOOLE_0:def 3, NUMBERS:def 1;
    end;
  end;
  [i,0] <> +infty & [i,0] <> -infty by Th4;
  then not [i,0] in {+infty,-infty} by TARSKI:def 2;
  then not [i,0] in REAL \/ {+infty,-infty} by A1, XBOOLE_0:def 3;
  hence not [i,0] in ExtREAL by NUMBERS:def 5, XXREAL_0:def 2,def 3;
end;

registration
  let i be Nat;
  cluster [i,0] -> non ext-real;
  coherence by Th6, XXREAL_0:def 1;
end;

definition
  func NaN -> object equals [1,0];
  coherence;
end;

theorem
  not NaN in ExtREAL by Th6;

reserve beta for valid_radix Real;
reserve p for positive Nat;
reserve eMin for negative ExtInt;
reserve eMax for positive ExtNat;

definition
  let b, p, eMin, eMax;
  func FLOAT_NUMBER(b, p, eMin, eMax) -> Subset of ExtREAL equals
  {x where x is Real : ex M, e being Integer
   st x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
   & eMin - 1 < e & e <= eMax } \/ {+infty, -infty};
  coherence
  proof
    set NUM = {x where x is Real : ex M, e being Integer
    st x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
    & eMin - 1 < e & e <= eMax };
    set IT = NUM \/ {+infty, -infty};
    for x being object st x in IT holds x in ExtREAL
    proof
      let x be object;
      assume Z1: x in IT;
      then per cases by XBOOLE_0:def 3;
      suppose A1: x in NUM;
        then ex y being Real st x = y & ex M, e being Integer
        st y = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
        & eMin - 1 < e & e <= eMax;
        then x in REAL by XREAL_0:def 1;
        hence x in ExtREAL by XBOOLE_0:def 3, XXREAL_0:def 4;
      end;
      suppose A2: x in {-infty, +infty};
        hence x in ExtREAL by XBOOLE_0:def 3, XXREAL_0:def 4;
      end;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;

theorem ThZeroInFloatNumber:
  0 in FLOAT_NUMBER(b, p, eMin, eMax)
proof
  set NUM = {x where x is Real : ex M, e being Integer
  st x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
  & eMin - 1 < e & e <= eMax };
  ex M, e being Integer st 0 = M * (b to_power (e - p + 1))
  & |.M.| <= b to_power (p-1) & eMin - 1 < e & e <= eMax
  proof
    set M = 0, e = 0;
    take M, e;
    thus 0 = M * (b to_power (e - p + 1));
    thus |.M.| <= b to_power (p-1) by COMPLEX1:44;
    thus eMin - 1 < e;
    thus e <= eMax;
  end;
  then 0 in NUM;
  hence thesis by XBOOLE_0:def 3;
end;

registration
  let b, p, eMin, eMax;
  cluster real for Element of FLOAT_NUMBER(b, p, eMin, eMax);
  existence
  proof
    reconsider v=0 as Element of FLOAT_NUMBER(b, p, eMin, eMax)
    by ThZeroInFloatNumber;
    take v;
    thus thesis;
  end;
  cluster -> ext-real for Element of FLOAT_NUMBER(b, p, eMin, eMax);
  coherence
  proof
    let x be Element of FLOAT_NUMBER(b, p, eMin, eMax);
    x in ExtREAL by TARSKI:def 3;
    hence thesis;
  end;
end;

theorem St3200:
  -infty in FLOAT_NUMBER(b, p, eMin, eMax)
  & +infty in FLOAT_NUMBER(b, p, eMin, eMax)
proof
  -infty in {-infty,+infty} by TARSKI:def 2;
  hence -infty in FLOAT_NUMBER(b, p, eMin, eMax) by XBOOLE_0:def 3;
  +infty in {-infty,+infty} by TARSKI:def 2;
  hence +infty in FLOAT_NUMBER(b, p, eMin, eMax) by XBOOLE_0:def 3;
end;

theorem Th11:
  for x being Real
  holds x in FLOAT_NUMBER(b, p, eMin, eMax) iff
  (ex M, e being Integer
  st x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
  & eMin - 1 < e & e <= eMax)
proof
  set NUM = {x where x is Real : ex M, e being Integer
   st x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
   & eMin - 1 < e & e <= eMax };
  let x be Real;
  thus x in FLOAT_NUMBER(b, p, eMin, eMax) implies
  (ex M, e being Integer
  st x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
  & eMin - 1 < e & e <= eMax)
  proof
    assume A1: x in FLOAT_NUMBER(b, p, eMin, eMax);
    then x in NUM or x in {+infty, -infty} by XBOOLE_0:def 3;
    then ex y being Real st x = y & ex M, e being Integer
    st y = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
    & eMin - 1 < e & e <= eMax by TARSKI:def 2;
    hence thesis;
  end;
  thus (ex M, e being Integer
  st x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
  & eMin - 1 < e & e <= eMax) implies x in FLOAT_NUMBER(b, p, eMin, eMax)
  proof
    given M, e being Integer such that 
    A1: x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
    & eMin - 1 < e & e <= eMax;
    x in NUM by A1;
    hence x in FLOAT_NUMBER(b, p, eMin, eMax) by XBOOLE_0:def 3;
  end;
end;

Lm7:
  for y being ExtReal st y in REAL
  holds y in FLOAT_NUMBER(b, p, eMin, eMax)
  implies (- y) in FLOAT_NUMBER(b, p, eMin, eMax)
proof
  let y be ExtReal;
  assume A2: y in REAL;
  assume y in FLOAT_NUMBER(b, p, eMin, eMax);
  then consider M, e being Integer such that
  A3: y = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
  & eMin - 1 < e & e <= eMax
  by A2, Th11;
  A4: (- M) * (b to_power (e - p + 1))
  = - (M * (b to_power (e - p + 1)))
  .= (-1) * ((M * (b to_power (e - p + 1))) qua ExtReal)
  .= (-1) * (y) by A3
  .= - y by XXREAL_3:91;
  |.(- M).| = |.M.| by COMPLEX1:52;
  hence (- y) in FLOAT_NUMBER(b, p, eMin, eMax) by A3,A4,Th11;
end;

theorem ThFloatClosedUnderNegation:
  for x being ExtReal
  holds x in FLOAT_NUMBER(b, p, eMin, eMax)
  iff (- x) in FLOAT_NUMBER(b, p, eMin, eMax)
proof
  let x be ExtReal;
  A1: - -infty = +infty & - +infty = -infty by XXREAL_3:def 3;
  x in REAL \/ {+infty, -infty} by XXREAL_0:def 1,def 4;
  then x in REAL or x in {+infty, -infty} by XBOOLE_0:def 3;
  then per cases by TARSKI:def 2;
  suppose CASE1: x in REAL;
    thus x in FLOAT_NUMBER(b, p, eMin, eMax)
    implies (- x) in FLOAT_NUMBER(b, p, eMin, eMax) by CASE1,Lm7;
  
    A2: - x in REAL by CASE1, XREAL_0:def 1;
    assume (- x) in FLOAT_NUMBER(b, p, eMin, eMax);
    then - (- x) = x & - (- x) in FLOAT_NUMBER(b, p, eMin, eMax) by Lm7,A2;
    hence x in FLOAT_NUMBER(b, p, eMin, eMax);
    hence thesis by Lm7;
  end;
  suppose CASE2: x=+infty;
    hence thesis by A1, CASE2, St3200;
  end;
  suppose CASE3: x=-infty;
    hence thesis by A1, CASE3, St3200;
  end;
end;

theorem ThFiniteFloatClosedUnderNegation:
  for x being Real
  holds x in FLOAT_NUMBER(b, p, eMin, eMax)
  iff (- x) in FLOAT_NUMBER(b, p, eMin, eMax)
proof
  let x be Real;
  reconsider y=x as ExtReal;
  thus x in FLOAT_NUMBER(b, p, eMin, eMax)
  implies -x in FLOAT_NUMBER(b, p, eMin, eMax)
  proof
    assume x in FLOAT_NUMBER(b, p, eMin, eMax);
    then -y in FLOAT_NUMBER(b, p, eMin, eMax) by ThFloatClosedUnderNegation;
    hence (- x) in FLOAT_NUMBER(b, p, eMin, eMax)
    by XXREAL_3:def 3;
  end;
  assume (- x) in FLOAT_NUMBER(b, p, eMin, eMax);
  then -y in FLOAT_NUMBER(b, p, eMin, eMax) by XXREAL_3:def 3;
  hence x in FLOAT_NUMBER(b, p, eMin, eMax) by ThFloatClosedUnderNegation;
end;

theorem Th12:
  for x being positive Real
  holds x in FLOAT_NUMBER(b, p, eMin, eMax) iff
  (ex M, e being Integer
  st x = M * (b to_power (e - p + 1)) & 0 < M & M <= b to_power (p-1)
  & eMin - 1 < e & e <= eMax)
proof
  let x be positive Real;
  thus x in FLOAT_NUMBER(b, p, eMin, eMax) implies (ex M, e being Integer
  st x = M * (b to_power (e - p + 1)) & 0 < M & M <= b to_power (p-1)
  & eMin - 1 < e & e <= eMax)
  proof
    assume x in FLOAT_NUMBER(b, p, eMin, eMax);
    then consider M, e being Integer such that
    A1: x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
    & eMin - 1 < e & e <= eMax by Th11;
    take M, e;
    thus thesis by A1, COMPLEX1:43;
  end;
  given M, e being Integer such that 
  A2: x = M * (b to_power (e - p + 1)) & 0 < M & M <= b to_power (p-1) & eMin - 1 < e & e <= eMax;
  |.M.| = M by A2, COMPLEX1:43;
  hence thesis by A2, Th11;
end;

theorem Th17:
  for b being valid_radix Nat
  holds 1 in FLOAT_NUMBER(b, p, eMin, eMax)
proof
  let b be valid_radix Nat;
  1 is positive Real;
  ex M, e being Integer
  st 1 = M * (b to_power (e - p + 1)) & 0 < M & M <= b to_power (p-1)
  & eMin - 1 < e & e <= eMax
  proof
    p >= 0 + 1 by INT_1:7;
    then A1: p - 1 in NAT by INT_1:5;
    then reconsider p1=p - 1 as Nat;
    b to_power (p - 1) = b |^ p1 by POWER:41;
    then reconsider M = b to_power (p - 1) as Integer;
    take M;
    take e = 0;
    thus M * (b to_power (e - p + 1))
          = (b to_power (p - 1)) * (b to_power (e - p + 1))
         .= b to_power ((p - 1) + (e - p + 1)) by POWER:27
         .= 1 by POWER:24;
    thus 0 < M;
    thus M <= b to_power (p-1);
    thus eMin - 1 < e;
    thus e <= eMax;
  end;
  hence 1 in FLOAT_NUMBER(b, p, eMin, eMax) by Th12;
end;

definition
  let beta, p, eMin, eMax;
  func FLOAT(beta, p, eMin, eMax) -> set equals
  FLOAT_NUMBER(beta, p, eMin, eMax) \/ {-0, NaN};
  coherence;
end;

registration
  let b, p, eMin, eMax;
  cluster ext-real for Element of FLOAT(b, p, eMin, eMax);
  existence
  proof
    reconsider v=0 as Element of FLOAT_NUMBER(b, p, eMin, eMax)
    by ThZeroInFloatNumber;
    v in FLOAT(b, p, eMin, eMax) by XBOOLE_0:def 3;
    then reconsider v as Element of FLOAT(b, p, eMin, eMax);
    take v;
    thus thesis;
  end;
end;

theorem St3300:
  for eMin being negative Integer
  for x being ExtReal
  st x in FLOAT_NUMBER(b, p, eMin, eMax)
   & x > 0
   holds b to_power (eMin - p + 1) <= x
proof
  let eMin be negative Integer;
  let x be ExtReal;
  assume A1: x in FLOAT_NUMBER(b, p, eMin, eMax);
  assume A2: x > 0;
  x in REAL \/ {+infty,-infty} by XXREAL_0:def 1,def 4;
  then x in REAL or x in {+infty,-infty} by XBOOLE_0:def 3;
  then per cases by TARSKI:def 2;
  suppose x in REAL;
    then reconsider X=x as positive Real by A2;
    consider M, e being Integer such that
    A3: X = M * (b to_power (e - p + 1)) & 0 < M & M <= b to_power (p-1)
    & eMin - 1 < e & e <= eMax by A1, Th12;
    eMin - 1 + 1 <= e by A3, INT_1:7;
    then eMin + (1 - p) <= e + (1 - p) by XREAL_1:6;
    then A4: (b to_power (eMin - p + 1)) <= (b to_power (e - p + 1))
    by Def1, Th1;
    0 + 1 <= M by A3, INT_1:7;
    then 1 * (b to_power (e - p + 1)) <= M * (b to_power (e - p + 1))
    by XREAL_1:64;
    hence (b to_power (eMin - p + 1)) <= x by A3, A4, XXREAL_0:2;
  end;
  suppose x=+infty;
    hence b to_power (eMin - p + 1) <= x by XREAL_0:def 1, XXREAL_0:9;
  end;
  suppose x=-infty;
    hence thesis by A2;
  end;
end;

theorem St3301:
  for eMin being negative Integer
  for x being Real
  st x in FLOAT_NUMBER(b, p, eMin, eMax) & b in INT
  ex n being Integer
  st n*(b to_power (eMin - p + 1)) = x
proof
  let eMin be negative Integer;
  let x be Real;
  assume A1: x in FLOAT_NUMBER(b, p, eMin, eMax);
  assume b in INT;
  then A2: b in NAT by INT_1:3;
  consider M, e being Integer such that
  A3: x = M * (b to_power (e - p + 1)) & |.M.| <= b to_power (p-1)
  & eMin - 1 < e & e <= eMax by A1,Th11;
  (eMin - 1) + 1 <= e by A3, INT_1:7;
  then eMin - eMin <= e - eMin by XREAL_1:9;
  then e - eMin in NAT by INT_1:3;
  then b to_power (e - eMin) in NAT by A2, ThNatPowers;
  then reconsider n = M * (b to_power (e - eMin)) as Integer;
  take n;
  thus n*(b to_power (eMin - p + 1))
   = (M * (b to_power (e - eMin))) * (b to_power (eMin - p + 1))
  .= M * ((b to_power (e - eMin)) * (b to_power (eMin - p + 1)))
  .= M * (b to_power ((e - eMin) + (eMin - p + 1))) by POWER:27
  .= M * (b to_power (e - p + 1))
  .= x by A3;
end;

theorem ThSmallestPositiveFloat:
  for eMin being negative Integer
  for b being valid_radix Nat
  for x being positive Real
  st x in FLOAT_NUMBER(b, p, eMin, eMax)
  holds (b to_power (eMin - p + 1)) <= x
proof
  let eMin be negative Integer;
  let b be valid_radix Nat;
  let x be positive Real;
  assume A1: x in FLOAT_NUMBER(b, p, eMin, eMax);
  b in NAT by ORDINAL1:def 12;
  then consider n being Integer such that
  A2: n*(b to_power (eMin - p + 1)) = x
  by A1, St3301, NUMBERS:17;
  n is positive by A2, Def1, POWER:34;
  then 0 + 1 <= n by INT_1:7;
  then 1 * (b to_power (eMin - p + 1)) <= n * (b to_power (eMin - p + 1))
  by XREAL_1:64;
  hence (b to_power (eMin - p + 1)) <= x by A2;
end;

theorem ThSmallestNegativeFloat:
  for eMin being negative Integer
  for b being valid_radix Nat
  for x being negative Real
  st x in FLOAT_NUMBER(b, p, eMin, eMax)
  holds x <= -(b to_power (eMin - p + 1))
proof
  let eMin be negative Integer;
  let b be valid_radix Nat;
  let x be negative Real;
  assume A1: x in FLOAT_NUMBER(b, p, eMin, eMax);
  b in NAT by ORDINAL1:def 12;
  then consider n being Integer such that
  A2: n*(b to_power (eMin - p + 1)) = x
  by A1, St3301, NUMBERS:17;
  n is negative by A2;
  then n <= -1 by INT_1:8;
  then n * (b to_power (eMin - p + 1)) <= (-1) * (b to_power (eMin - p + 1))
  by XREAL_1:64;
  hence x <= - (b to_power (eMin - p + 1)) by A2;
end;

definition
  let b, p, eMin, eMax;
  let IT be Element of FLOAT(b,p,eMin,eMax);
  attr IT is normal means :Def8:
  IT <> 0 & IT in FLOAT_NUMBER(b,p,eMin,eMax) if eMin =-infty,
  ex iMin being negative Integer st iMin = eMin
  & ex x being Real st x = IT
  & b to_power iMin <= |.x.| if eMin <> -infty & eMax = +infty
  otherwise ex iMin being negative Integer st iMin = eMin
  & ex iMax being positive Integer st iMax = eMax
  & ex x being Real st x = IT
  & b to_power iMin <= |.x.|
  & |.x.| <= (b to_power iMax)*(b - (b to_power (1 - p)));
  consistency;
end;

theorem ThNormalInFloatNumber:
  for v being Element of FLOAT(b, p, eMin, eMax)
  st v is normal
  holds v in FLOAT_NUMBER(b, p, eMin, eMax)
proof
  let v be Element of FLOAT(b, p, eMin, eMax);
  assume A1: v is normal;
  then per cases;
  suppose eMin = -infty;
    hence v in FLOAT_NUMBER(b, p, eMin, eMax) by A1, Def8;
  end;
  suppose eMin <> -infty & eMax = +infty;
    then ex iMin being negative Integer st iMin = eMin
    & ex x being Real st x = v
    & b to_power iMin <= |.x.| by A1, Def8;
    then v in REAL by XREAL_0:def 1;
    then A2: v <> -0 & v <> NaN by Th6, NUMBERS:31;
    v in FLOAT_NUMBER(b,p,eMin,eMax) or v in {-0, NaN} by XBOOLE_0:def 3;
    hence thesis by A2, TARSKI:def 2;
  end;
  suppose eMin <> -infty & eMax <> +infty;
    then ex iMin being negative Integer st iMin = eMin
    & ex iMax being positive Integer st iMax = eMax
    & ex x being Real st x = v
    & b to_power iMin <= |.x.|
    & |.x.| <= (b to_power iMax)*(b - (b to_power (1 - p))) by A1,Def8;
    then v in REAL by XREAL_0:def 1;
    then A2: v <> -0 & v <> NaN by Th6, NUMBERS:31;
    v in FLOAT_NUMBER(b,p,eMin,eMax) or v in {-0, NaN} by XBOOLE_0:def 3;
    hence thesis by A2, TARSKI:def 2;
  end;
end;

registration
  let b, p, eMin, eMax;
  cluster normal -> ext-real for Element of FLOAT(b,p,eMin,eMax);
  coherence
  proof
    let x be Element of FLOAT(b, p, eMin, eMax);
    assume x is normal;
    then x in FLOAT_NUMBER(b, p, eMin, eMax) by ThNormalInFloatNumber;
    hence thesis;
  end;
end;

theorem ThNormalFloatCriteriaFiniteExp:
  for eMin being negative Integer
  for eMax being positive Integer
  for v being Element of FLOAT(b, p, eMin, eMax)
  holds v is normal iff ex x being Real st x = v
  & b to_power eMin <= |.x.|
  & |.x.| <= (b to_power eMax)*(b - (b to_power (1 - p)))
proof
  let eMin be negative Integer;
  let eMax be positive Integer;
  let v be Element of FLOAT(b, p, eMin, eMax);
  thus v is normal implies ex x being Real st x = v
  & b to_power eMin <= |.x.|
  & |.x.| <= (b to_power eMax)*(b - (b to_power (1 - p)))
  proof
    assume v is normal;
    then ex iMin being negative Integer st iMin = eMin
    & ex iMax being positive Integer st iMax = eMax
    & ex x being Real st x = v
    & b to_power iMin <= |.x.|
    & |.x.| <= (b to_power iMax)*(b - (b to_power (1 - p))) by Def8;
    hence thesis;
  end;
  given x being Real such that 
  A1: x = v & b to_power eMin <= |.x.| & |.x.| <= (b to_power eMax)*(b - (b to_power (1 - p)));
  thus v is normal by A1, Def8;
end;

Lm19:
  b to_power (1 - p) <= 1
  & 1 <= b to_power (p - 1)
proof
  A1: p >= 0 + 1 by INT_1:7;
  then 1 - p <= 1 - 1 by XREAL_1:13;
  then b to_power (1 - p) <= b to_power 0 by Def1,Th1;
  hence b to_power (1 - p) <= 1 by POWER:24;
  1 - 1 <= p - 1 by A1, XREAL_1:9;
  then b to_power 0 <= b to_power (p - 1) by Def1,Th1;
  hence 1 <= b to_power (p - 1) by POWER:24;
end;

Lm20:
  for eMax being positive Integer
  holds b <= b to_power eMax
proof
  let eMax be positive Integer;
  0 + 1 <= eMax by INT_1:7;
  then b to_power 1 <= b to_power eMax by Def1,Th1;
  hence b <= b to_power eMax by POWER:25;
end;

Lm21:
  for eMax being positive Integer
  holds b * (b - 1) <= (b to_power eMax)*(b - (b to_power (1 - p)))
proof
  let eMax be positive Integer;
  A1: 0 <= b - 1 & 0 <= b
  proof
    b > 1 by Def1;
    hence 0 <= b - 1 by XREAL_1:48;
    thus 0 <= b;
  end;
  1 >= b to_power (1 - p) by Lm19;
  then A2: b - 1 <= b - (b to_power (1 - p)) by XREAL_1:13;
  b <= b to_power eMax by Lm20;
  hence b * (b - 1) <= (b to_power eMax)*(b - (b to_power (1 - p)))
  by A1, A2, XREAL_1:66;
end;

theorem ThNormalFloatInNegInfty:
  for x being Element of FLOAT(b, p, eMin, eMax)
  st x=1 & eMin=-infty
  holds x is normal
proof
  let x be Element of FLOAT(b, p, eMin, eMax);
  assume A1: x=1;
  assume A2: eMin=-infty;
  1 in NAT;
  then x in REAL by A1, NUMBERS:19;
  then x <> -0 & x <> NaN by Th6, NUMBERS:31;
  then not x in {-0, NaN} by TARSKI:def 2;
  then x in FLOAT_NUMBER(b, p, eMin, eMax) by XBOOLE_0:def 3;
  hence x is normal by A1,A2,Def8;
end;

theorem ThNormalFloatInPosInfty:
  for x being Element of FLOAT(b, p, eMin, eMax)
  st x=1 & eMax=+infty
  holds x is normal
proof
  let x be Element of FLOAT(b, p, eMin, eMax);
  assume A1: x=1;
  assume A2: eMax=+infty;
  per cases;
  suppose eMin=-infty;
    hence thesis by ThNormalFloatInNegInfty,A1;
  end;
  suppose A3: eMin <> -infty;
    then reconsider iMin = eMin as negative Integer by ThNegExtInt;
    reconsider x1=x as Real by A1;
    b > 1 by Def1;
    then b to_power iMin < |.x1.| by A1,COMPLEX1:48,POWER:36;
    hence x is normal by A2,Def8;
  end;
end;

theorem ThExistenceNormalFloat:
  for b being valid_radix Nat
  ex x being Element of FLOAT(b, p, eMin, eMax)
  st x is normal
proof
  let b be valid_radix Nat;
  1 in FLOAT_NUMBER(b, p, eMin, eMax) by Th17; then
  A1: 1 in FLOAT(b, p, eMin, eMax) by XBOOLE_0:def 3;
  per cases;
  suppose eMin=-infty;
    hence thesis by A1, ThNormalFloatInNegInfty;
  end;
  suppose eMax=+infty;
    hence thesis by A1, ThNormalFloatInPosInfty;
  end;
  suppose A2: eMin <> -infty & eMax <> +infty;
    then reconsider iMin=eMin as negative Integer by ThNegExtInt;
    reconsider iMax=eMax as positive Integer by A2, ThPosExtInt;
    reconsider x0 = 1 as Element of FLOAT(b, p, eMin, eMax) by A1;
    take x0;
    ex x being Real st x = x0
    & b to_power iMin <= |.x.|
    & |.x.| <= (b to_power iMax)*(b - (b to_power (1 - p)))
    proof
      take x = 1;
      thus x = x0;
      b > 1 & iMin < 0 by Def1;
      hence b to_power iMin <= |.x.| by COMPLEX1:48, POWER:36;
      1 < b by Def1;
      then A3: 1 + 1 <= b by INT_1:7;
      then 2 + (- 1) <= b + (- 1) by XREAL_1:6;
      then A4: 2 * 1 <= b * (b - 1) by A3, XREAL_1:66;
      b * (b - 1) <= (b to_power iMax)*(b - (b to_power (1 - p))) by Lm21;
      then 2 <= (b to_power iMax)*(b - (b to_power (1 - p))) by A4, XXREAL_0:2;
      hence |.x.| <= (b to_power iMax)*(b - (b to_power (1 - p)))
      by COMPLEX1:48, XXREAL_0:2;
    end;
    hence x0 is normal by Def8;
  end;
end;

registration
  let b be valid_radix Nat, p, eMin, eMax;
  cluster normal for Element of FLOAT(b,p,eMin,eMax);
  existence by ThExistenceNormalFloat;
end;

definition
  let b, p, eMin, eMax;
  let IT be Element of FLOAT(b,p,eMin,eMax);
  attr IT is subnormal means :Def9:
  ex x being Real st x = IT &
  ex iMin being negative Integer st iMin = eMin
  & 0 < |.x.| & |.x.| < b to_power iMin;
end;

registration
  let b, p, eMin, eMax;
  cluster subnormal -> real for Element of FLOAT(b, p, eMin, eMax);
  coherence;
end;

theorem ThRealFloatsAreNumbers:
  for x being Real
  holds x in FLOAT_NUMBER(b, p, eMin, eMax)
  iff x in FLOAT(b, p, eMin, eMax)
proof
  let x be Real;
  thus x in FLOAT_NUMBER(b, p, eMin, eMax)
  implies x in FLOAT(b, p, eMin, eMax) by XBOOLE_0:def 3;
  thus x in FLOAT(b, p, eMin, eMax)
  implies x in FLOAT_NUMBER(b, p, eMin, eMax)
  proof
    assume A1: x in FLOAT(b, p, eMin, eMax);
    not -0 in ExtREAL & not NaN in ExtREAL by Th6;
    then {-0, NaN} misses ExtREAL by ZFMISC_1:51;
    then A2: {-0, NaN} /\ REAL = {} by XBOOLE_1:63, NUMBERS:31, XBOOLE_0:def 7;
    x in REAL by XREAL_0:def 1;
    then not x in {-0, NaN} by A2,XBOOLE_0:def 4;
    hence x in FLOAT_NUMBER(b, p, eMin, eMax) by A1,XBOOLE_0:def 3;
  end;
end;

theorem
  p = 1 implies
  for b being valid_radix Nat
  for eMin being negative Integer
  holds not ex v being Element of FLOAT(b, p, eMin, eMax)
  st v is subnormal
proof
  assume A1: p = 1;
  let b be valid_radix Nat;
  let eMin be negative Integer;
  set x = b to_power (eMin - p + 1);
  b > 1 by Def1;
  then x > 0 by POWER:34;
  then reconsider x as positive Real;
  A2: x = b to_power eMin by A1;
  then for v being positive Real st v in FLOAT_NUMBER(b, p, eMin, eMax)
  holds x <= v by ThSmallestPositiveFloat;

  for v being Element of FLOAT(b, p, eMin, eMax)
  holds not v is subnormal
  proof
    let v be Element of FLOAT(b, p, eMin, eMax);
    assume v is subnormal;
    then consider x0 being Real such that
    A3: x0 = v & 0 < |.x0.| & |.x0.| < b to_power eMin
    by Def9;
    A4: |.x0.| < x by A2,A3;
    A5: x0 in FLOAT_NUMBER(b, p, eMin, eMax)
    by A3, ThRealFloatsAreNumbers, XREAL_0:def 1;
    0 < -x0 or 0 < x0 by A3, COMPLEX1:71;
    then per cases;
    suppose x0 is positive;
      then (b to_power (eMin - p + 1)) <= x0 by A5,ThSmallestPositiveFloat;
      hence contradiction by A1, A2, A4, COMPLEX1:43;
    end;
    suppose A6: x0 is negative;
      then -(- x) <= -x0 by A5, ThSmallestNegativeFloat, XREAL_1:24;
      hence contradiction by A4, A6, COMPLEX1:70;
    end;
  end;
  hence thesis;
end;

theorem
  eMin = -infty implies not ex v being Element of FLOAT(b,p,eMin,eMax)
  st v is subnormal by Def9;

theorem
  for x being Element of FLOAT(b, p, eMin, eMax)
  st x = 0
  holds not x is normal & not x is subnormal
proof
  let x be Element of FLOAT(b, p, eMin, eMax);
  assume A1: x = 0;
  thus not x is normal
  proof
    assume A2: x is normal;
    per cases;
    suppose eMin = -infty;
      then x <> 0 by A2, Def8;
      hence contradiction by A1;
    end;
    suppose eMin <> -infty & eMax = +infty;
      then ex iMin being negative Integer st
      iMin = eMin
      & ex x0 being Real st x0 = x
      & b to_power iMin <= |.x0.| by A2, Def8;
      then 0 < |.0.| by A1, Def1;
      hence contradiction by COMPLEX1:44;
    end;
    suppose eMin <> -infty & eMax <> +infty;
      then ex iMin being negative Integer st iMin = eMin
      & ex iMax being positive Integer st iMax = eMax
      & ex x0 being Real st x0 = x
      & b to_power iMin <= |.x0.|
      & |.x0.| <= (b to_power iMax)*(b - (b to_power (1 - p)))
      by A2, Def8;
      then 0 < |.0.| by A1;
      hence contradiction by COMPLEX1:44;
    end;
  end;
  thus not x is subnormal
  proof
    assume x is subnormal;
    then ex x0 being Real st x0 = x &
    ex iMin being negative Integer st iMin = eMin
    & 0 < |.x0.| & |.x0.| < b to_power iMin by Def9;
    then 0 < |.0.| by A1;
    hence contradiction by COMPLEX1:44;
  end;
end;

theorem ThBabyExistenceSubnormalFloat:
  p > 1 implies
  for b being valid_radix Nat
  for eMin being negative Integer
  ex v being Element of FLOAT(b, p, eMin, eMax)
  st v = (b to_power (1 - p))*(b to_power eMin)
  & v is subnormal
proof
  assume A1: p > 1;
  let b be valid_radix Nat;
  let eMin be negative Integer;
  ex v being Element of FLOAT(b, p, eMin, eMax)
  st v = (b to_power (1 - p))*(b to_power eMin)
  proof
    set M = 1, e = eMin;
    set x = M * (b to_power (e - p + 1));
    x > 0;
    then reconsider x as positive Real;
    eMin <= e & -1 < 0;
    then eMin + (- 1) < e + 0 by XREAL_1:8;
    then eMin - 1 < e;
    then B1: x = M * (b to_power (e - p + 1))
    & 0 < M & M <= b to_power (p - 1)
    & eMin - 1 < e & e <= eMax  by Lm19, XXREAL_0:2;
    then x in FLOAT_NUMBER(b,p,eMin,eMax) by Th12;
    then reconsider v=x as Element of FLOAT(b,p,eMin,eMax) by XBOOLE_0:def 3;
    take v;
    x = b to_power ((1 - p) + e)
     .= (b to_power (1 - p)) * (b to_power e) by POWER:27;
    hence thesis;
  end;
  then consider v being Element of FLOAT(b, p, eMin, eMax) such that
  A2: v = (b to_power (1 - p))*(b to_power eMin);
  take v;
  thus v = (b to_power (1 - p))*(b to_power eMin) by A2;
  ex x being Real st x = v & 0 < |.x.| & |.x.| < b to_power eMin
  proof
    reconsider x = (b to_power (1 - p))*(b to_power eMin) as Real;
    B1: v in FLOAT(b, p, eMin, eMax) & x = v by A2;
    take x;
    thus x = v by B1;
    B2: b > 1 by Def1;
    B3: x = (b to_power (1 - p))*(b to_power eMin)
         .= b to_power (1 - p + eMin) by POWER:27;
    x > 0 by B2,POWER:34;
    then B4: |.x.| = x by COMPLEX1:43;
    hence 0 < |.x.|;
    p > 1 by A1;
    then 1 + 1 <= p by INT_1:7;
    then 2 <= p;
    then 1 - p <= 1 - 2 by XREAL_1:10;
    then 1 - p <= -1;
    then 1 - p < 0 & eMin <= eMin by XXREAL_0:1;
    then 1 - p + eMin < 0 + eMin by XREAL_1:8;
    then 1 - p + eMin < eMin;
    then b to_power (1 - p + eMin) < b to_power eMin by B2, POWER:39;
    hence |.x.| < b to_power eMin by B3,B4;
  end;
  hence v is subnormal by Def9;
end;

theorem ThExistenceSubnormalFloat:
  p > 1 implies
  for b being valid_radix Nat
  for eMin being negative Integer
  ex v being Element of FLOAT(b, p, eMin, eMax)
  st v is subnormal
proof
  assume A1: p > 1;
  let b be valid_radix Nat;
  let eMin be negative Integer;
  ex v being Element of FLOAT(b, p, eMin, eMax) st
  v = (b to_power (1 - p))*(b to_power eMin)
  & v is subnormal
  by A1, ThBabyExistenceSubnormalFloat;
  hence thesis;
end;
